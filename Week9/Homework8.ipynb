{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 25,
   "metadata": {},
   "outputs": [],
   "source": [
    "from functools import reduce\n",
    "from py_ecc import bn128\n",
    "from py_ecc.bn128 import G1, G2, bn128_curve, curve_order, multiply, eq, neg, field_modulus, pairing\n",
    "from sage.all import *\n",
    "\n",
    "F = GF(field_modulus)\n",
    "F_x.<x> = F[] # polynomial over finite field\n",
    "\n",
    "A = Matrix(F, [\n",
    "    [0,0,1,0,0,0,0,0],\n",
    "    [0,0,0,0,1,0,0,0],\n",
    "    [0,0,0,0,0,0,1,0]\n",
    "])\n",
    "\n",
    "B = Matrix(F, [\n",
    "    [0,0,0,1,0,0,0,0],\n",
    "    [0,0,0,0,0,1,0,0],\n",
    "    [0,0,0,0,0,0,0,1]\n",
    "])\n",
    "\n",
    "C = Matrix(F,[\n",
    "    [0,0,0,0,0,0,1,0],\n",
    "    [0,0,0,0,0,0,0,1],\n",
    "    [0,1,0,0,0,0,0,0]\n",
    "])\n",
    "\n",
    "# x*2 + x + 3\n",
    "\n",
    "x =  F.random_element()\n",
    "y =  F.random_element()\n",
    "z =  F.random_element()\n",
    "u =  F.random_element()\n",
    "\n",
    "out = x * y * z * u\n",
    "v1 = x*y\n",
    "v2 = z*u\n",
    "\n",
    "# create the witness vector\n",
    "w = vector(F, [1, out, x, y, z, u, v1, v2])\n",
    "\n",
    "\n",
    "assert Matrix(C * w) == Matrix(A * w).elementwise_product(Matrix(B * w))\n",
    "\n",
    "def trusted_setup(m, t_x, U_polys, V_polys, W_polys):\n",
    "    encrypted_G1 = list()\n",
    "    encrypted_G2 = list()\n",
    "    t_x_enc = list()\n",
    "\n",
    "    tau = F.random_element()\n",
    "    pow_of_tau = 1  # tau ** 0\n",
    "\n",
    "    alpha = F.random_element()\n",
    "    beta = F.random_element()\n",
    "    Alpha = multiply(G1, int(alpha))\n",
    "    Beta = multiply(G2, int(beta))\n",
    "    t_x_eval = t_x(tau)\n",
    "\n",
    "    # pow_tau_C = list()\n",
    "    for i in range(0, m):\n",
    "        temp = multiply(G1, int(pow_of_tau))\n",
    "        t_x_enc.append(multiply(temp, int(t_x_eval)))\n",
    "        encrypted_G1.append(temp)\n",
    "        encrypted_G2.append(multiply(G2, int(pow_of_tau)))\n",
    "\n",
    "\n",
    "        # pow_tau_C.append(multiply(G1, int(beta * U_polys[i](pow_of_tau) + alpha *\n",
    "        #                               V_polys[i](pow_of_tau) + W_polys[i](pow_of_tau))))\n",
    "        pow_of_tau = pow_of_tau*tau\n",
    "\n",
    "    # pow_tau_C_sum = reduce(bn128_curve.add, pow_tau_C)\n",
    "\n",
    "    return encrypted_G1, encrypted_G2, t_x_enc, Alpha, Beta\n",
    "\n",
    "def generate_target_polynomial(n):\n",
    "    # Create a polynomial of the form (x - 1)(x - 2)(x - 3)...(x - n)\n",
    "    x = var('x')\n",
    "    res = F_x(prod([(x - i) for i in range(1, n+1)]))\n",
    "    for i in range(1, n+1):\n",
    "        assert res(i) == 0, \"Invalid target polynom\"\n",
    "    return res\n",
    "\n",
    "def interpolate_polynomials(matrix):\n",
    "    result = list()\n",
    "    for column in matrix.columns():\n",
    "        polynomial = F_x.lagrange_polynomial([(index+1, values) for index, values in enumerate(column)])\n",
    "        result.append(polynomial)\n",
    "    return result\n",
    "\n",
    "def inner_product_polynomials_with_witness(polys, witness):\n",
    "    def mul(x, y): return x * y\n",
    "    def sum(x, y): return x + y\n",
    "    return reduce(sum, map(mul, polys, witness))\n",
    "\n",
    "def inner_product_enc_polynomials_with_witness(polys, witness):\n",
    "    def mul(x, y): return multiply(x, int(y))\n",
    "    return reduce(bn128_curve.add, map(mul, polys, witness))\n",
    "\n",
    "def encrypt_polynomials(polynomials, encrypted_points):\n",
    "    encrypted_polynomials = [None] * len(polynomials)\n",
    "    for i, poly in enumerate(polynomials):\n",
    "        if poly != 0:\n",
    "            for j, coeff in enumerate(poly.coefficients(sparse=False)):\n",
    "                encrypted_polynomials[i] = bn128_curve.add(encrypted_polynomials[i], multiply(encrypted_points[j], int(coeff)))\n",
    "    return encrypted_polynomials\n",
    "\n",
    "def encrypt_hx_tx(h_x, t_x_enc):\n",
    "    h_x_enc = list()\n",
    "    for index, coeff in enumerate(h_x.coefficients(sparse=False)):\n",
    "        h_x_enc.append(multiply(t_x_enc[index], int(coeff)))\n",
    "    return reduce(bn128_curve.add, h_x_enc)\n",
    "\n",
    "t_x = generate_target_polynomial(len(A.rows()))\n",
    "U_polys = interpolate_polynomials(A)\n",
    "V_polys = interpolate_polynomials(B)\n",
    "W_polys = interpolate_polynomials(C)\n",
    "\n",
    "(encrypted_G1, encrypted_G2, t_x_enc, Alpha, Beta) = trusted_setup(len(A.rows()), t_x, U_polys, V_polys, W_polys)\n",
    "\n",
    "poly1 = inner_product_polynomials_with_witness(U_polys, w)\n",
    "poly2 = inner_product_polynomials_with_witness(V_polys, w)\n",
    "poly3 = inner_product_polynomials_with_witness(W_polys, w)  \n",
    "\n",
    "h_x, remainder = ((poly1 * poly2) - poly3).quo_rem(t_x)\n",
    "assert remainder == 0, \"Division has a ramainder\"\n",
    "\n",
    "hx_tx_enc = encrypt_hx_tx(h_x, t_x_enc)\n",
    "\n",
    "A_1 = inner_product_enc_polynomials_with_witness(encrypt_polynomials(U_polys, encrypted_G1), w)\n",
    "B_2 = inner_product_enc_polynomials_with_witness(encrypt_polynomials(V_polys, encrypted_G2), w)\n",
    "C_1 = inner_product_enc_polynomials_with_witness(encrypt_polynomials(W_polys, encrypted_G1), w)\n",
    "\n",
    "\n",
    "\n",
    "# A_1 = bn128_curve.add(encrypted_U_polys, Alpha)\n",
    "# B_2 = bn128_curve.add(encrypted_U_polys, Beta)\n",
    "C_1 = bn128_curve.add(C_1, hx_tx_enc)\n",
    "\n",
    "# assert pairing(B_2, A_1) == pairing(Beta, Alpha) + pairing(G2, C_1), \"Fail\"\n",
    "pairing_1 = pairing(B_2, A_1) \n",
    "pairing_2 = pairing(G2, C_1)\n",
    "\n",
    "print(pairing_1)\n",
    "print(pairing_2)\n",
    "\n",
    "assert pairing_1 == pairing_2, \"Fail\"\n"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "SageMath 9.8",
   "language": "sage",
   "name": "sagemath"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.11.6"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
